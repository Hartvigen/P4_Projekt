COMPILER MAGIA

    /*
    public SymbolTable   tab;
    public CodeGenerator gen;
    */
  
/*--------------------------------------------------------------------------*/
CHARACTERS
  letter = "ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz".
  digit = "0123456789".
  cr  = '\r'.
  lf  = '\n'.
  tab = '\t'.
  CharInLine = ANY - '\r' - '\n'.
  AnyButDoubleQuote = CharInLine - '\"'.

TOKENS
  IDENT  = letter {letter | digit}.
  NUMBER = digit {digit} | digit {digit} '.' digit {digit}.
  TEXT = '"' {AnyButDoubleQuote | "\\\""} '"'.
  NONE = "none".
  TRUE = "true".
  FALSE = "false".
  RPAREN = ')'.
  
COMMENTS FROM "/*" TO "*/" NESTED
COMMENTS FROM "//" TO lf

IGNORE cr + lf + tab



PRODUCTIONS
/*------------------------------------------------------------------------*/
MAGIA =                             (. HeadNode headNode; mainNode = new MAGIA(); .)
    {SYNC Head<out headNode>        (. mainNode.Add(headNode);                    .)
    } 
    Stmts 
    {FuncDecl}.
    
/*------------------------------------------------------------------------*/
Head<out HeadNode headNode> (. headNode = null; .) 
    = '[' 
    ( "vertex"              (. headNode = new HeadNode(HeadNode.VERTEX); .)
    | "edge"                (. headNode = new HeadNode(HeadNode.EDGE);   .)
    ) 
    WEAK '(' AttrDecls      (. int i = 1; .)
    WEAK ')' ']'.

/*------------------------------------------------------------------------*/
AttrDecls
    = AttrDecl {WEAK ',' AttrDecl}.

/*------------------------------------------------------------------------*/
AttrDecl
    = Type IDENT [Assign].


/*------------------------------------------------------------------------*/
FuncDecl
    = SYNC "func" IDENT '(' [IF(la.kind != _RPAREN) FuncParams] ')' SYNC '{' Stmts SYNC '}'.

/*------------------------------------------------------------------------*/
FuncParams
    = Type IDENT {SYNC ',' Type IDENT}.


/*------------------------------------------------------------------------*/
Stmts
    = SYNC {Stmt | StrucStmt}.

/*------------------------------------------------------------------------*/
StrucStmt
    = "while" '(' Expr ')' '{' Stmts '}'
    | "for" '(' Stmt ',' Expr ',' Stmt ')' '{' Stmts '}'
    | "foreach" '(' Type IDENT "in" Expr ')' '{' Stmts '}'
    | "if" '(' Expr ')' '{' Stmts '}' {"elseif" '(' Expr ')' '{' Stmts '}'} ["else" '{' Stmts '}'].

/*------------------------------------------------------------------------*/
Stmt
    = (FullDecl | CallOrID {Member} IdentCont | "return" Expr).

/*------------------------------------------------------------------------*/
IdentCont
    = Assign | EdgeOpr EdgeOneOrMore.

/*------------------------------------------------------------------------*/
Member
    = WEAK '.' CallOrID.

/*------------------------------------------------------------------------*/
FullDecl
    = Type (IDENT [Assign] | '{' VtxDecls '}' | VtxDecl).

/*------------------------------------------------------------------------*/
Assign
    = '=' (Expr | '{' Args '}').

/*------------------------------------------------------------------------*/
VtxDecls
    = VtxDecl {WEAK ',' VtxDecl}.

/*------------------------------------------------------------------------*/
VtxDecl
    = '(' IDENT VEParams ')'.

/*------------------------------------------------------------------------*/
EdgeOpr
    = ("<-" | "--" | "->").

/*------------------------------------------------------------------------*/
EdgeOneOrMore
    = (IDENT | EdgeDecl | '{' EdgeDecls '}').

/*------------------------------------------------------------------------*/
EdgeDecls 
    = EdgeDecl {WEAK ',' EdgeDecl}.

/*------------------------------------------------------------------------*/
EdgeDecl 
    = '(' IDENT VEParams ')'.


/*------------------------------------------------------------------------*/
VEParams
    = {WEAK ',' IDENT Assign}.


/*------------------------------------------------------------------------*/
Expr
    = ExprOR.
    
/*------------------------------------------------------------------------*/
ExprOR
    = ExprAnd {"||" ExprAnd}.

/*------------------------------------------------------------------------*/
ExprAnd
    = ExprEQ {"&&" ExprEQ}.

/*------------------------------------------------------------------------*/
ExprEQ
    = ExprRel [("==" | "!=") ExprRel].

/*------------------------------------------------------------------------*/
ExprRel
    = ExprPlus [('<' | '>' | "<=" | ">=") ExprPlus].

/*------------------------------------------------------------------------*/
ExprPlus
    = ['-'] ExprMult {('+' | '-') ExprMult}.

/*------------------------------------------------------------------------*/
ExprMult
    = ExprNot {('*' | '/' | '%') ExprNot}.

/*------------------------------------------------------------------------*/
ExprNot
    = ['!'] Factor.

/*------------------------------------------------------------------------*/
Factor
    = Const | ('(' Expr ')' | CallOrID) {Member}.


/*------------------------------------------------------------------------*/
CallOrID
    = IDENT ['(' Args ')'].

/*------------------------------------------------------------------------*/
Args
    = [Expr {WEAK ',' Expr}].


/*------------------------------------------------------------------------*/
Const
    = NUMBER
    | TEXT
    | TRUE
    | FALSE
    | NONE.

/*------------------------------------------------------------------------*/
Type
    = SingleType
    | CollecType.

/*------------------------------------------------------------------------*/
CollecType
    = "list" WEAK '<' SingleType WEAK '>'
    | "set" WEAK '<' SingleType WEAK '>'
    | "queue" WEAK '<' SingleType WEAK '>'
    | "stack" WEAK '<' SingleType WEAK '>'.

/*------------------------------------------------------------------------*/
SingleType
    = "number"
    | "bool"
    | "text"
    | "vertex"
    | "edge".
/*------------------------------------------------------------------------*/

END MAGIA.
