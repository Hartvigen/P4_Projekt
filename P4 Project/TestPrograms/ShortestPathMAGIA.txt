[vertex(number distance = 1000000000, vertex parent)]
[edge(number weight = 1)]

/*-------------------------------------------------------------------------*/

vertex endNode = Initialize()
Print()

set<vertex> processed = set<vertex> { }
set<vertex> remaining = GetVertices()

while (!IsEmpty(remaining)) {
    vertex current = ExtractMin(remaining)
    Add(processed, current)
    current.style = "bold"
    foreach (vertex v in GetAdjacent(current)) {
        Relax(current, v)
    }
    Print()
    current.style = "none"
}

HighlightPath(endNode)
Print()

/*-------------------------------------------------------------------------*/

func none Relax(vertex start, vertex end) {
    number edgeLength = GetEdge(start, end).weight
    if (end.distance > start.distance + edgeLength) {
        end.distance = start.distance + edgeLength
        end.label = AsText(end.distance)
        end.parent = start
    }
}

func vertex ExtractMin(set<vertex> remaining) {
    vertex smallest = none

    foreach (vertex v in remaining) {
        if (smallest == none) {
            smallest = v
        }
        elseif (v.distance < smallest.distance) {
            smallest = v
        }
    }

    Remove(remaining, smallest)
    return smallest
}

func none HighlightPath(vertex endNode) {
    if (endNode.parent != none) {
        GetEdge(endNode.parent, endNode).style = "bold"
        HighlightPath(endNode.parent)
    }
}

func vertex Initialize() {
    vertex {
        (A, label = "A", distance = 0),
        (v1),
        (v2),
        (v3),
        (v4),
        (v5),
        (v6),
        (B, label = "B")
    }

    A -- { (v1), (v4) }
    v2 -- { (v1), (v3) }
    v3 -- { (v4), (v5) }
    v5 -- { (B), (v4), (v6) }
    v6 -- { (B) }

    return B
}
