/* This file contains the Token specification and Grammar which COCO/R uses to construct the scanner
and parser. COCO/R uses Extend Bacus-Naur form (EBNF) for the grammar, as well as its own series of commands
which will be explained when met first in context.  */

/* The EBNF uses the following extensions: */
/* [] Brackets mean that the string is written zero or one times.  */

/* {} Curly brackets mean that the string is repeated zero or more times  */

/* () is a grouping used alongside |. The parenthesis contains a series of strings seperated by |. */
/* This means that exactly one of the strings is chosen. */

/* The required start of a COCO/R grammar file, while the "MAGIA" part specifies */
/* the starting symbol of the grammar.*/
COMPILER MAGIA   


/*public CodeGenerator gen;*/
    
  
/*--------------------------------------------------------------------------*/
/* The following section contains the token specification of MAGIA.*/
CHARACTERS
  letter = "ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz".
  digit = "0123456789".
  cr  = '\r'.
  lf  = '\n'.
  tab = '\t'.
  CharInLine = ANY - cr - lf.                   /* 'ANY' refers to all chars in UTF-8. */
  AnyButDoubleQuote = CharInLine - '\"'.        /* Any char that is not a double quote or new-line */

TOKENS
  IDENT  = letter {letter | digit | '_'}.
  NUMBER = digit {digit} ['.' digit {digit}].
  TEXT = '"' {AnyButDoubleQuote | "\\\""} '"'.  /* The string  \\\"  is an escaped backslash (\\) and double qoute (\") */
  
COMMENTS FROM "/*" TO "*/" NESTED               /* Nested means: "There can be comments inside comments" */
COMMENTS FROM "//" TO lf

IGNORE cr + lf + tab                            /* These characters are ignored by the scanner */


/*------------------------------------------------------------------------*/
/* The rest of the file contains the grammar for MAGIA.  */
PRODUCTIONS
/* The starting rule of MAGIA contains quite a few COCO/R-specific features. The code written */
/* in (. .) is C# code that is injected into the generated parser code. */
MAGIA =                                     (. tab = tab.OpenScope(); mainNode = null; Block mainBlock = new Block(); .)
/* The keyword 'SYNC' specifies that the parser will skip tokens untill it discovers a token that */
/* should follow the SYNC statement. If the next token is already valid, the SYNC is skipped. */

/*<> contains the attributes of the grammar production which can be injected into and out of the production. */
/* The keyword 'out' indicates that whatever happens to the variable 'headNode' inside the Head */
/* production will reflect on the variable in this production. The parameter MUST be initialized */
/* inside the Head production through semantic operations. */
    {SYNC Head<out HeadNode headNode>       (. mainBlock.Add(headNode); .)
    } 
/* The keyword 'ref' is simply "pass by reference", so that changes to the members of the parameter */
/* also happen to the variable outside the Stmts production */
    Stmts<ref mainBlock>
    { FuncDecl<out FuncDeclNode funcNode>   (. mainBlock.Add(funcNode); .)
    }                                       (. mainNode = new MAGIA(mainBlock); tab = tab.CloseScope(); .)
/* All productions end with a dot. */
    .
    
/*------------------------------------------------------------------------*/
Head<out HeadNode headNode>              (. headNode = null; .) 
    = '[' 
    ( "vertex"                           (. headNode = new HeadNode(HeadNode.VERTEX); .)
    | "edge"                             (. headNode = new HeadNode(HeadNode.EDGE);   .)
    ) 
/* The keyword 'WEAK' declares that the parser should expect some token T, but if some other token */
/* is encountered instead, it should give an error as usual, but continue as if nothing is wrong. */
    WEAK '(' AttrDecls<ref headNode>
    WEAK ')' ']'.

/*------------------------------------------------------------------------*/
AttrDecls<ref HeadNode headNode> =          (. VarDeclNode attrDecl; .)
/* The keyword '?' after headNode indicates that a null-check should be performed before attempting to call the function */
    AttrDecl<out attrDecl>                  (. headNode?.AddAttr(attrDecl); .)
    {
        WEAK ',' AttrDecl<out attrDecl>     (. headNode?.AddAttr(attrDecl); .)
    }.

/*------------------------------------------------------------------------*/
AttrDecl<out VarDeclNode attrDecl> = (. attrDecl = null; string name = ""; ExprNode val = null; .)
    Type<out BaseType typ>
    /* The expression t.val contains the scanned string value of the most recently parsed token.*/
    IDENT                            (. name = t.val; .)
    [
        Assign<out val> 
    ]                                (. attrDecl = new VarDeclNode(typ, name, val); .)
    .


/*The IF command makes the parser read a token ahead to see whether the function declaration has any parameters or if it is simply an empty pair of parenthisis. If a right parenthisis is read, there is no parameters, so FuncParams can safely be skipped, but if any non-right parenthisis is read, then FuncParam will be called and any eventual errors will be handled there*/
/*------------------------------------------------------------------------*/
FuncDecl<out FuncDeclNode funcNode> =                       (. funcNode = null; Obj funcObj; BaseType returnType = null; BaseTypeList parameterTypes = new BaseTypeList(); string funcName = ""; Block paramBlock = new Block(); Block stmtBlock = new Block(); .)
    SYNC "func"                                             (. tab = tab.OpenScope(); .)
    (Type<out returnType> | "none") IDENT                     (. funcName = t.val; .)
    '('
        [
            IF(la.val != ")") FuncParams<ref paramBlock, ref parameterTypes>
        ]
    ')' 
    SYNC '{'
        Stmts<ref stmtBlock>
    SYNC '}'                                                (. SymbolTable funcScope = tab;
                                                               tab = tab.CloseScope(); 
                                                               funcObj = tab.NewObj(funcName, new FunctionType(returnType, parameterTypes), func); 
                                                               funcNode = new FuncDeclNode(funcName, funcObj, paramBlock, stmtBlock); 
                                                            .)
    .

/*------------------------------------------------------------------------*/
FuncParams<ref Block paramBlock, ref BaseTypeList parameterTypes> = (. BaseType type = null; .)       
    Type<out type>                          (. parameterTypes.Add(type); .)
    IDENT                                   (. paramBlock.Add(new VarDeclNode(type, t.val, null)); tab.NewObj(t.val, type, var); .)
    {
        SYNC ',' Type<out type>             (. parameterTypes.Add(type); .)
        IDENT                               (. paramBlock.Add(new VarDeclNode(type, t.val, null)); tab.NewObj(t.val, type, var); .)
    }.										


/*------------------------------------------------------------------------*/
Stmts<ref Block block> =
    SYNC {
          FullDecl<ref block>
        | Stmt<out StmtNode stmtNode>       (. block.Add(stmtNode); .)
        | StrucStmt<out StmtNode stmtNode>  (. block.Add(stmtNode); .)
    }.

/*------------------------------------------------------------------------*/
StrucStmt<out StmtNode s>                   (. s = null; .)
    = StmtWhile<out s>
    | StmtFor<out s>
    | StmtForeach<out s>
    | StmtIf<out s>.

/*------------------------------------------------------------------------*/
StmtWhile<out StmtNode w>                   (. w = null; Block b = new Block(); .)
    = "while" '(' Expr<out ExprNode e>      (. tab = tab.OpenScope(); .)
        ')' '{' Stmts<ref b>                (. w = new WhileNode(e, b); .)
        '}'									(. tab = tab.CloseScope(); .)
		.

/*------------------------------------------------------------------------*/
StmtFor<out StmtNode f>                     (. f = null; StmtNode init = null; ExprNode e = null; StmtNode iter = null; Block b = new Block(); tab = tab.OpenScope(); .)
    = "for" '(' Stmt<out StmtNode s1>       (. init = s1; .)
        ',' Expr<out ExprNode e1>           (. e = e1;  .)
        ',' Stmt<out StmtNode s2>           (. iter = s2; .)
        ')' '{' Stmts<ref b>                (. f = new ForNode(init, e, iter, b); .)
        '}'									(. tab = tab.CloseScope(); .)
		.

/*------------------------------------------------------------------------*/
StmtForeach<out StmtNode f>                 (. f = null; Block b = new Block(); VarDeclNode v = null; tab = tab.OpenScope(); .)
    = "foreach" '(' Type<out BaseType typ>
        IDENT                               (. v = new VarDeclNode(typ, t.val, null); .)
        "in" Expr<out ExprNode e1>			
        ')' '{' Stmts<ref b>                (. f = new ForeachNode(v, e1, b); .)
        '}'									(. tab = tab.CloseScope(); .)
		.

/*------------------------------------------------------------------------*/
StmtIf<out StmtNode i>                                  (. i = null; ExprNode e = null; Block b = new Block(); IfNode j = null; IfNode k = null; .)
    = "if" '(' Expr<out ExprNode ie1>                   (. e = ie1; tab = tab.OpenScope(); .)
        ')' '{' Stmts<ref b>                            (. i = new IfNode(e, b); j = (IfNode)i; .)
        '}' {"elseif" '(' Expr<out ExprNode ie2>        (. e = ie2; b = new Block(); .)
        ')' '{' Stmts<ref b>                            (. k = new IfNode(e, b); j.SetElse(k); j = k; .)
        '}'} ["else"                                    (. b = new Block(); .)
        '{' Stmts<ref b>                                (. k = new IfNode(null, b); j.SetElse(k); .)
        '}']											(. tab = tab.CloseScope(); .)
		.

/*------------------------------------------------------------------------*/
Stmt<out StmtNode stmtNode> =                       (. stmtNode = null; .)
    ( 
        CallOrID<out IdentNode i>                   (. stmtNode = new LoneCallNode(i); .)
        { 
            Member<i, out MemberNode member>        (. i = member; stmtNode = new LoneCallNode(i); .)
        } 
        [IdentCont<i, out StmtNode stmt>            (. stmtNode = stmt; .)
        ]            
    | 
        "return" Expr<out ExprNode expr>            (. stmtNode = new ReturnNode(expr); .)
    |
        "break"                                     (. stmtNode = new BreakNode(); .)
    |
        "continue"                                  (. stmtNode = new ContinueNode(); .)
    ).

/*------------------------------------------------------------------------*/
/* More than one parameter can be injected into a production, such as with IdentCont and EdgeOneOrMore */
IdentCont<IdentNode i, out StmtNode s>              (. s = null; Block b = new Block(); .)
    = Assign<out ExprNode expr>                     (. s = new AssignNode(i, expr); .)
     | EdgeOpr<out int op>                      
     EdgeOneOrMore<i, op, ref b>                    (. s = b; .)
     .


/*------------------------------------------------------------------------*/
Member<ExprNode source, out MemberNode mem> =       (. mem = null; .)
    WEAK '.' CallOrID<out IdentNode i>              (. mem = new MemberNode(source, i); .)
    .

/*------------------------------------------------------------------------*/
FullDecl<ref Block block> =						(. Obj obj; .)
    Type<out BaseType type>
    (
        IDENT                                   (. string name = t.val; ExprNode expr = null; .)
        [
            Assign<out expr>
        ]                                       (. block.Add(new VarDeclNode(type, name, expr)); obj = tab.NewObj(name, type, var); .)
    | 
        '{' 
            VtxDecls<ref block>
        '}' 
    | 
        VtxDecl<out VertexDeclNode VertexDecl>  (. block.Add(VertexDecl); .)
    ).

/*------------------------------------------------------------------------*/
Assign<out ExprNode expr>                       (. expr = null; .)
    = '=' 
    (Expr<out expr>
    | '{' Args<out CollecConst collec>          (. expr = collec; .)
    '}'
    ).

/*------------------------------------------------------------------------*/
VtxDecls<ref Block b>                            
    = VtxDecl<out VertexDeclNode v1>            (. b.Add(v1); .)
    {WEAK ',' VtxDecl<out VertexDeclNode v2>    (. b.Add(v2); .)
    }.

/*------------------------------------------------------------------------*/
VtxDecl<out VertexDeclNode v>                   (. v = null; Obj obj; .)
    = '(' IDENT                                 (. v = new VertexDeclNode(new VertexType(), t.val); obj = tab.NewObj(t.val, new VertexType(), var); .)
    VEParams<v>               
    ')'.


/*------------------------------------------------------------------------*/
/* Operators is a class in the solution, containing enumerate values for all operators in MAGIA. */
EdgeOpr<out int op> (. op = 0;                  .)
    = ("<-"         (. op = Operators.LEFTARR;  .)
    | "--"          (. op = Operators.NONARR;   .)
    | "->"          (. op = Operators.RIGHTARR; .)
    ).

/*------------------------------------------------------------------------*/
EdgeOneOrMore<IdentNode left, int op, ref Block b>  
    = (Identifier<out VarNode varNode>                  (. b.Add(new EdgeDeclNode(left, varNode, op)); .)
    | EdgeDecl<left, op, out EdgeDeclNode edge>         (. b.Add(edge); .)
    | '{' EdgeDecls<left, op, ref b>                
    '}').

/*------------------------------------------------------------------------*/
EdgeDecls<IdentNode left, int op, ref Block b>
    = EdgeDecl<left, op, out EdgeDeclNode e1>           (. b.Add(e1); .)
    {WEAK ',' EdgeDecl<left, op, out EdgeDeclNode e2>   (. b.Add(e2); .)
    }.

/*------------------------------------------------------------------------*/
EdgeDecl<IdentNode left, int op, out EdgeDeclNode edge> (. edge = null; .)
    = '(' Identifier<out VarNode right>                 (. edge = new EdgeDeclNode(left, right, op); .)
    VEParams<edge>                           
    ')'.


/*------------------------------------------------------------------------*/
VEParams<VEDeclNode ve>
    = {WEAK ',' Identifier<out VarNode varNode>
     Assign<out ExprNode expr>                          (. ve.AddAttr(new AssignNode(varNode, expr)); .)
     }.


/*------------------------------------------------------------------------*/
Expr<out ExprNode e>                (. e = null; .)
    = ExprOR<out e>.
    
/*------------------------------------------------------------------------*/
ExprOR<out ExprNode e>              (. e = null; int op = 0; .)
    = ExprAnd<out ExprNode e1>      (. e = e1; .)
    {"||"                           (. op  = Operators.OR; .)
    ExprAnd<out ExprNode e2>        (. e = new BinExprNode(e, op, e2); .)                                
    }.

/*------------------------------------------------------------------------*/
ExprAnd<out ExprNode e>             (. e = null; int op = 0; .)
    = ExprEQ<out ExprNode e1>       (. e = e1; .)
    {"&&"                           (. op = Operators.AND; .)
    ExprEQ<out ExprNode e2>         (. e = new BinExprNode(e, op, e2); .)
    }.

/*------------------------------------------------------------------------*/
ExprEQ<out ExprNode e>              (. e = null; int op = 0; .)
    = ExprRel<out ExprNode e1>      (. e = e1; .)
    [("=="                          (. op = Operators.EQ; .)
    | "!="                          (. op = Operators.NEQ; .)
    ) ExprRel<out ExprNode e2>      (. e = new BinExprNode(e, op, e2); .)
    ].

/*------------------------------------------------------------------------*/
ExprRel<out ExprNode e>             (. e = null; int op = 0; .)
    = ExprPlus<out ExprNode e1>     (. e = e1; .)
    [('<'                           (. op = Operators.LESS; .)
    | '>'                           (. op = Operators.GREATER; .)
    | "<="                          (. op = Operators.LESSEQ; .)
    | ">="                          (. op = Operators.GREATEQ; .)
    ) ExprPlus<out ExprNode e2>     (. e = new BinExprNode(e, op, e2); .)
    ].

/*------------------------------------------------------------------------*/
ExprPlus<out ExprNode e>            (. e = null; bool b = false; int op = 0; .)
    = ['-'                          (. b = true; .)
    ] ExprMult<out ExprNode e1>     (. if(b) e = new UnaExprNode(Operators.UMIN, e1); else e = e1; .)
    {('+'                           (. op = Operators.PLUS; .)
    | '-'                           (. op = Operators.BIMIN; .)
    ) ExprMult<out ExprNode e2>     (. e = new BinExprNode(e, op, e2); .)
    }.

/*------------------------------------------------------------------------*/
ExprMult<out ExprNode e>            (. e = null; int op = 0;.)
    = ExprNot<out ExprNode e1>      (. e = e1; .)
    {('*'                           (. op = Operators.MULT; .)
    | '/'                           (. op = Operators.DIV; .)
    | '%'                           (. op = Operators.MOD; .)
    ) ExprNot<out ExprNode e2>      (. e = new BinExprNode(e, op, e2); .)
    }.

/*------------------------------------------------------------------------*/
ExprNot<out ExprNode e>             (. e = null; bool b = false;.)
    = ['!'                          (. b = true; .)
    ] Factor<out e>                 (. if(b) e = new UnaExprNode(Operators.NOT, e); .)
    .

/*------------------------------------------------------------------------*/
Factor<out ExprNode e>                      (. e = null; .)
    =
    Const<out e>
    | 
    (
        '(' 
            Expr<out e> 
        ')' 
        | 
        CallOrID<out IdentNode ident>       (. e = ident; .)
    ) 
    {
        Member<e, out MemberNode member>    (. e = member; .)
    }
    .        


/*------------------------------------------------------------------------*/
CallOrID<out IdentNode i> =                 (. i = null; .)
    Identifier<out VarNode varNode>         (. i = varNode; .)
    [
        '(' 
            Args<out CollecConst collec>    (. i = new CallNode(i.identifier, collec); .)
        ')'
    ].

/*------------------------------------------------------------------------*/
Args<out CollecConst collec> =      (. collec = new CollecConst(); ExprNode expr; .)
    [
        Expr<out expr>              (. collec.Add(expr); .)
        {
            WEAK ',' Expr<out expr> (. collec.Add(expr); .)
        }
    ].


/*------------------------------------------------------------------------*/
Identifier<out VarNode varNode> =   (. varNode = null; .)
    (
        IDENT                       (. varNode = new VarNode(t.val); .)
    ).

/*------------------------------------------------------------------------*/
Const<out ExprNode e> =     (. e = null; .)
    ( NUMBER                (. e = new NumConst(Convert.ToDouble(t.val, System.Globalization.CultureInfo.InvariantCulture));    .)
    | TEXT                  (. e = new TextConst(t.val);                    .)
    | "true"                (. e = new BoolConst(Convert.ToBoolean(t.val)); .)
    | "false"               (. e = new BoolConst(Convert.ToBoolean(t.val)); .)
    | "none"                (. e = new NoneConst();                         .)
    ).

/*------------------------------------------------------------------------*/
Type<out BaseType type> =        (. type = null; .)
    ( SingleType<out type>
    | CollecType<out type>
    ).

/*------------------------------------------------------------------------*/
/* Types is, like Operators, a file in the solution containing enumerate values for all types. */
CollecType<out BaseType type> =                                (. type = null; BaseType subType = null; .)
    ( "list" WEAK '<' SingleType<out subType> WEAK '>'         (. type = new ListType(subType);  .)
    | "set" WEAK '<' SingleType<out subType> WEAK '>'          (. type = new SetType(subType);   .)
    | "queue" WEAK '<' SingleType<out subType> WEAK '>'        (. type = new QueueType(subType); .)
    | "stack" WEAK '<' SingleType<out subType> WEAK '>'        (. type = new StackType(subType); .)
    ).
/*------------------------------------------------------------------------*/
SingleType<out BaseType type> = (. type = null; .)
    ( "number"                  (. type = new NumberType();  .)
    | "boolean"                 (. type = new BooleanType(); .)
    | "text"                    (. type = new TextType();    .)
    | "vertex"                  (. type = new VertexType();  .)
    | "edge"                    (. type = new EdgeType();    .)
    ).

/*------------------------------------------------------------------------*/

END MAGIA.